apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.getkeepsafe.dexcount'

android {

    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        applicationId "net.kibotu.android.recyclerviewpresenter.app"
        minSdkVersion 21
        targetSdkVersion targetSdkVer
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
    }

    // endregion

    // region dex options

    dexOptions {
        javaMaxHeapSize "4g"
        maxProcessCount 8
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        exclude 'META-INF/library-core_release.kotlin_module'
    }

    // endregion
}

dependencies {
    implementation libs.kotlinJdk8
    implementation libs.supportAppCompat
    implementation libs.appCompatResources
    implementation libs.supportDesign
    implementation libs.constraintLayout
    implementation libs.supportRecyclerview
    implementation libs.recyclerviewAnimators
    implementation libs.swipeRefreshLayout
    implementation libs.supportCardView
    implementation libs.glide
    kapt libs.glideAnnotationsProcessor

    implementation libs.androidKtxCore
    implementation libs.activityKtx
    implementation libs.androidKtxCollection
    implementation libs.fragmentKtx

    implementation libs.pagingCommon
    implementation libs.pagingRuntimeKtx

    implementation libs.lifecycleLivedataKtx

    implementation project(':lib')

    implementation(libs.exozetCore) {
        exclude module: 'RecyclerViewPresenter'
    }
    implementation libs.logger

    implementation 'com.github.JastAir:Android-ProgressView:0.1'
    debugImplementation libs.leakcanary2
    implementation libs.okhttp4

}

// region override support library version

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
    resolutionStrategy.force libs.kotlinJdk8
    resolutionStrategy.force libs.supportAppCompat
    resolutionStrategy.force libs.appCompatResources
    resolutionStrategy.force libs.constraintLayout
    resolutionStrategy.force libs.supportRecyclerview
    resolutionStrategy.force libs.recyclerviewAnimators
    resolutionStrategy.force libs.supportCardView
    resolutionStrategy.force libs.okhttp4
}

// endregion